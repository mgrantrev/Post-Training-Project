plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.0'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.revastudio'
version = '0.0.1-SNAPSHOT'
description = 'Media Manager for Team MACS'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	
	implementation 'org.postgresql:postgresql'
	
	// make sure rest assured is before any junit dependencies, per the documentation
	testImplementation 'io.rest-assured:rest-assured:6.0.0'

    runtimeOnly 'com.h2database:h2:2.4.240'
	testImplementation 'io.cucumber:cucumber-java:7.33.0'
	testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.33.0'
	testImplementation 'io.cucumber:cucumber-spring:7.33.0'
	testImplementation 'org.junit.jupiter:junit-jupiter:5.14.0'
	testImplementation 'org.junit.platform:junit-platform-suite:1.14.1'
	
	// Selenium & Playwright (Playwright is used in frontend, but we keep selenium for cucumber if needed)
	implementation 'org.seleniumhq.selenium:selenium-java:4.39.0'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	
	implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'
}

tasks.named('test') {
	useJUnitPlatform()
	exclude '**/CucumberRunner.class'
}

tasks.register('e2eTest', Test) {
	useJUnitPlatform()
	include '**/CucumberRunner.class'
	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath
	description = 'Runs Cucumber E2E tests (requires running app and browser)'
	group = 'verification'
}

bootRun {
	def envFile = file('.env')
	if (envFile.exists()) {
		envFile.readLines().each { line ->
			if (line && !line.startsWith('#') && line.contains('=')) {
				def (key, value) = line.split('=', 2)
				environment key.trim(), value.trim()
			}
		}
	}
}
